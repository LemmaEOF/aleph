plugins {
	id 'com.unascribed.nilgradle' version '1.3.4'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = "${project.maven_group}"
archivesBaseName = "${project.archives_base_name}"
version = "${project.version}+${project.minecraft_version}"

repositories {
	mavenCentral()
	maven {
		url 'https://repo.sleeping.town'
		content {
			includeGroup 'com.unascribed'
		}
	}
	maven {
		url 'https://maven.fabricmc.net'
		content {
			includeGroup 'net.fabricmc'
		}
	}
	maven {
		url 'https://maven.quiltmc.org/repository/release'
		content {
			includeGroup 'org.quiltmc'
		}
	}
	maven {
		url 'https://libraries.minecraft.net'
		content {
			includeGroup 'com.mojang'
		}
	}
	maven {
		url 'https://maven.minecraftforge.net'
		content {
			includeGroup 'de.oceanlabs.mcp'
		}
	}
}

nil {
	id 'aleph'
	standalone true
}

dependencies {
	loader 'com.unascribed:nilloader:1.2.0'

	game nil.minecraft('1.18.2', 'client')

	implementation 'com.mojang:datafixerupper:4.1.27'
	implementation 'com.mojang:brigadier:1.0.18'
}

mappings {
	via 'org.quiltmc:hashed:1.18.2'

	work 'org.quiltmc:quilt-mappings:1.18.2+build.23:v2'

	target 'net.fabricmc:intermediary:1.18.2:v2'
	target nil.forgeFrankenmapping(nil.mojmap('1.18.2', 'client'), 'de.oceanlabs.mcp:mcp_config:1.18.2')
	target nil.mojmap('1.18.2', 'client')
}

task sourcesJar(type: Jar) {
	classifier 'sources'

	from sourceSets.main.allSource
	from 'src/lib/java'
}

publishing {
	repositories {
		if (project.hasProperty("publish-username")) {
			maven {
				url "https://repo-api.sleeping.town/"
				credentials {
					username project.hasProperty("publish-username") ? project.getProperty("publish-username") : null
					password project.hasProperty("publish-password") ? project.getProperty("publish-password") : null
				}
			}
		}
		maven {
			url file('build/maven').toURI().toString()
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			artifact jar
			artifact sourcesJar
		}
	}
}
